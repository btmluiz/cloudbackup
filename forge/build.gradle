plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "me.shedaniel.unified-publishing"
}

loom {
    forge {
        mixinConfig "cloudbackup.mixins.json"
        mixinConfig "cloudbackup-common.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"

    modImplementation("me.shedaniel.cloth:cloth-config-forge:${rootProject.cloth_config_version}")

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    shadowCommon minecraftLibraries('com.google.auth:google-auth-library-oauth2-http:1.11.0')
    shadowCommon minecraftLibraries('com.google.api-client:google-api-client:2.0.0')
    shadowCommon minecraftLibraries('com.google.oauth-client:google-oauth-client-jetty:1.34.1')
    shadowCommon minecraftLibraries('com.google.apis:google-api-services-drive:v3-rev20220815-2.0.0')
    shadowCommon minecraftLibraries('com.dropbox.core:dropbox-core-sdk:5.4.4')
    shadowCommon minecraftLibraries('org.yaml:snakeyaml:2.0') { transitive = false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    configurations = [project.configurations.shadowCommon]

    exclude("org/apache/**")
    exclude("javax/**")

    minimize {
        exclude(project(":common"))
        exclude(dependency("org.yaml:snakeyaml:.*"))
        exclude(dependency(group: "com.google", name: ".*"))
        exclude(dependency(group: "com.google.api-client", name: ".*"))
        exclude(dependency(group: "com.google.oauth-client", name: ".*"))
        exclude(dependency(group: "com.google.apis", name: ".*"))
    }

    relocate("com.google", "shadow.google")
    relocate("com.dropbox", "shadow.dropbox")
    relocate("org.yaml", "shadow.yaml")
    relocate("io.grpc", "shadow.io.grpc")
    relocate("io.opencensus", "shadow.io.opencensus")
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

task renameJarForPublication(type: Zip, dependsOn: remapJar) {
    from remapJar.archiveFile.map { zipTree(it) }
    extension "jar"
    metadataCharset "UTF-8"
    destinationDirectory = base.libsDirectory
    classifier project.name
}

assemble.dependsOn renameJarForPublication

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

unifiedPublishing {
    project {
        displayName = "[Forge $rootProject.supported_version] v$project.version"
        releaseType = "${rootProject.artifact_type}"
        gameVersions = ["${rootProject.supported_version}"]
        gameLoaders = ["forge"]
        mainPublication renameJarForPublication

        var CF_API_KEY = project.findProperty("CF_API_KEY") ?: System.getenv("CF_API_KEY")
        if (CF_API_KEY != null) {
            curseforge {
                token = CF_API_KEY
                id = rootProject.curseforge_id
            }
        }

        var MR_API_KEY = project.findProperty("MR_API_KEY") ?: System.getenv("MR_API_KEY")
        if (MR_API_KEY != null) {
            modrinth {
                token = MR_API_KEY
                id = rootProject.modrinth_id
            }
        }
    }
}
